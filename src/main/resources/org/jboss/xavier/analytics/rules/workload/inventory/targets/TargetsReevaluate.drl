package org.jboss.xavier.analytics.rules.workload.inventory.targets;

import org.jboss.xavier.analytics.pojo.output.workload.inventory.WorkloadInventoryReportModel;

dialect "java"
agenda-group "TargetsReevaluate"
lock-on-active true
auto-focus false

rule "Target_OCP_Reevaluate"
    when
        workloadInventoryReport : WorkloadInventoryReportModel(
            getRecommendedTargetsIMS() != null,
            getRecommendedTargetsIMS().contains(WorkloadInventoryReportModel.TARGET_OCP),
            getFlagsIMS() != null,
            getFlagsIMS().contains(WorkloadInventoryReportModel.SHARED_DISK_FLAG_NAME)
        )
    then
        modify(workloadInventoryReport)
        {
            getRecommendedTargetsIMS().remove(WorkloadInventoryReportModel.TARGET_OCP)
        }
end

rule "Target_None_Reevaluate"
    salience -1
    when
        workloadInventoryReport : WorkloadInventoryReportModel(
            eval( recommendedTargetsIMS == null || recommendedTargetsIMS.isEmpty())
        )
    then
        modify(workloadInventoryReport)
        {
            addRecommendedTargetsIMS("None")
        }
end
